#!/usr/bin/env expect

#
# \brief  Simple script to self-test Goa
# \author Sebastian Sumpf
# \date   2023-06-09
#

#
# tests name:[timeout]
#
set tests {
	hello_make
	hello_posix
	hello_meson
	hello_rust:60
	cmake_step2:40
	cmake_library
	qt5_core:100
	cmake_sdl2:40
	vfs/1-fs_query
	vfs/2-import
	vfs/3-vfs_server
	vfs/4-rump_ext2
	vfs/5-pipe
	vfs/6-terminal
	vfs/7-lwip
}

#
# expected output
#
set test_output(hello_make)       {\[init -> hello_make\] Hello}
set test_output(hello_posix)      {\[init -> hello_posix\] Hello POSIX}
set test_output(hello_meson)      {\[init -> hello_meson\] Hello Meson: 42}
set test_output(cmake_step2)      {\[init -> cmake_step2\] The square root of 24 is 4.89898}
set test_output(cmake_library)    {libforty_two.lib.so}
set test_output(qt5_core)         {\[init -> qt5_core\] Test done.}
set test_output(hello_rust)       {\[init -> hello_rust\] Hello Genode Rust world!}
set test_output(cmake_sdl2)       {\[init\] child "cmake_sdl2" exited with exit value 0}
set test_output(vfs/1-fs_query)   {\[init -> 1-fs_query -> report_rom\]   </listing>}
set test_output(vfs/2-import)     {child "2-import" exited with exit value 0}
set test_output(vfs/3-vfs_server) {<file name="verbose" size="2">no</file>}
set test_output(vfs/4-rump_ext2)  {Hello!Hello!}
set test_output(vfs/5-pipe)       {Hi, this is a pipe test!}
set test_output(vfs/6-terminal)   {Hi, this is a terminal test!}
set test_output(vfs/7-lwip)       {Received message with: Hi, there!}


proc _find_tool_dir { } {
	global goa

	set path $goa
	if {[file type $path] == "link"} {

		set link_target [file readlink $path]

		# resolve relative symlink used as symlink target
		if {[file pathtype $link_target] == "relative"} {
			set path [file join [file dirname $argv0] $link_target]
			set path [file normalize $path]
		} else {
			set path $link_target
		}
	}

	# strip binary name and 'bin/' path
	return [file dirname [file dirname $path]]
}

##
## From genode/tool/run adjusted for Goa
##

##
# Wait for a specific output of a already running spawned process
#
proc wait_for_output { wait_for_re timeout_value running_spawn_id } {
	global output
	global stats

	set timeout $timeout_value

	expect {
		-i $running_spawn_id -re $wait_for_re { }
		eof     { incr stats(failed); return "failed\t(spawned process died unexpectedly)" }
		timeout { incr stats(failed); return "failed\t(test execution timed out)" }
	}

	incr stats(success)
	return "success"
}


##
# Execute goa
#
# \param  wait_for_re    regular expression that matches the test completion
# \param  timeout_value  timeout in seconds
# \return result string
#
proc run_goa_until {{test} {timeout_value 0}} {
	global goa
	global example_dir
	global stats
	global test_output

	# check if test exists
	set test_dir [file join $example_dir $test]
	if {![file exists $test_dir]} {
		incr stats(skipped); return "skipped\t('$test_dir' does not exists)" }

	# execute and compare output
	variable public var
	set public [file join [_find_tool_dir] public]
	set var    [file join [_find_tool_dir] var]
	set tool   [file join $var tool]
	eval spawn $goa run -C $test_dir --public-dir $public --install-dir $tool --common-var-dir $var
	set result [wait_for_output $test_output($test) $timeout_value $spawn_id]

	#
	# leave depot and public intact
	#
	set test_name [lindex [split $test "/"] end]
	exec rm -rf var/$test_name

	return $result
}


##################
## Main program ##
##################

set goa         [exec which goa]
set tool_dir    [file join [_find_tool_dir] "share" "goa"]
set example_dir [file join [_find_tool_dir] "examples"]

set stats(success) 0
set stats(failed)  0
set stats(skipped) 0

source [file join $tool_dir lib util.tcl]

# measure duration
set start [clock milliseconds]

# run tests
set results { }
foreach test $tests {

	set timeout 20

	#retrieve possible timeout after ':'
	set test_args [split $test ":"]
	if {[lindex $test_args 1] != ""} {
		set test    [lindex $test_args 0]
		set timeout [lindex $test_args 1]
	}

	puts "\n--- $test ---"
	set result [run_goa_until $test $timeout]

	set col  [string repeat " " 25]
	set name [string replace $col 0 [string length $test] "$test: "]
	lappend results "$name$result"
}

set end [clock milliseconds]
set delta [expr ( $end - $start ) / 1000.0]


##
## Print results
##

puts "\n\n--- Finished after $delta sec ---"
foreach result $results {
	puts $result
}

set stats_output ""
append stats_output "\nsucceeded: $stats(success) failed: $stats(failed)" \
                    " skipped: $stats(skipped) total: [llength $tests]"
puts $stats_output
